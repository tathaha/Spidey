plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'nu.studer.jooq' version '5.2.1'
}

application {
    mainClassName = 'dev.mlnr.spidey.Spidey'
}

repositories {
    mavenCentral() // this is just to receive dependabot prs from mvnrepository
    jcenter()
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    // akinator wrapper
    implementation group: 'com.github.markozajc', name: 'akiwrapper', version: '1.5.1'

    // bot list handler
    implementation group: 'com.github.caneleex', name: 'BotListHandler', version: '1.0.12'

    // database
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.19'
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '4.0.3'
    implementation group: 'org.jooq', name: 'jooq', version: '3.14.8'
    jooqGenerator group: 'org.postgresql', name: 'postgresql', version: '42.2.19'

    // eval
    implementation group: 'org.codehaus.groovy', name: 'groovy-jsr223', version: '3.0.7'

    // expiring map for caches
    implementation group: 'net.jodah', name: 'expiringmap', version: '0.5.9'

    // jda
    implementation("net.dv8tion:JDA:4.2.0_239") {
        exclude group: 'club.minnced', module: 'opus-java'
    }

    // loading commands with reflection
    implementation group: 'io.github.classgraph', name: 'classgraph', version: '4.8.102'

    // logging
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha5'
    implementation group: 'io.sentry', name: 'sentry-logback', version: '4.3.0'

    // music
    implementation group: 'com.sedmelluq', name: 'lavaplayer', version: '1.3.73'
    implementation group: 'com.sedmelluq', name: 'jda-nas', version: '1.1.0'
}

jooq {
    version = '3.14.8'
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://localhost:5432/spidey'
                    user = 'sebo'
                    password = DB_PASSWORD
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        includes = '.*'
                        excludes = ''
                    }
                    generate {
                        records = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'dev.mlnr.spidey.jooq'
                        directory = "src/main/jooq"
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}

group = 'dev.mlnr'
description = 'Spidey'

shadowJar.setProperty('archiveFileName', 'Spidey.jar')
compileJava.options.encoding = 'UTF-8'