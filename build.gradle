plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'nu.studer.jooq' version '6.0.1'
}

application {
    mainClassName = 'dev.mlnr.spidey.Spidey'
}

repositories {
    mavenCentral()
    jcenter() // for jda-nas only
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    // akinator
    implementation group: 'com.github.markozajc', name: 'akiwrapper', version: '1.5.1.1'

    // bot list handler
    implementation group: 'dev.mlnr', name: 'BotListHandler-jda', version: '2.0.0_11'

    // database
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.24'
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '5.0.0'
    implementation group: 'org.jooq', name: 'jooq', version: '3.15.3'
    jooqGenerator group: 'org.postgresql', name: 'postgresql', version: '42.2.23'

    // eval
    implementation group: 'org.codehaus.groovy', name: 'groovy-jsr223', version: '3.0.9'

    // expiring map for caches
    implementation group: 'net.jodah', name: 'expiringmap', version: '0.5.10'

    // jda
    implementation("net.dv8tion:JDA:4.3.0_331") {
        exclude group: 'club.minnced', module: 'opus-java'
    }

    // loading commands with reflection
    implementation group: 'io.github.classgraph', name: 'classgraph', version: '4.8.116'

    // logging
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.0-alpha10'
    implementation group: 'io.sentry', name: 'sentry-logback', version: '5.2.0'

    // music
    implementation group: 'com.github.walkyst', name: 'lavaplayer-fork', version: '3a54cbd43b'
    implementation group: 'com.sedmelluq', name: 'jda-nas', version: '1.1.0'
}

jooq {
    version = '3.15.2'
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://localhost:5432/spidey'
                    user = 'sebo'
                    password = DB_PASSWORD
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        includes = '.*'
                        excludes = ''
                    }
                    generate {
                        records = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'dev.mlnr.spidey.jooq'
                        directory = "src/main/jooq"
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}

group = 'dev.mlnr'
description = 'Spidey'

shadowJar.setProperty('archiveFileName', 'Spidey.jar')
compileJava.options.encoding = 'UTF-8'
