plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'nu.studer.jooq' version '6.0.1'
}

application {
    mainClassName = 'dev.mlnr.spidey.Spidey'
}

repositories {
    mavenCentral()
    jcenter() // for jda-nas only
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    // akinator
    implementation group: 'com.github.markozajc', name: 'akiwrapper', version: '1.5.1.1'

    // bot list handler
    implementation group: 'dev.mlnr', name: 'BotListHandler-jda', version: '2.0.0_11'

    // database
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.3.1'
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '5.0.1'
    implementation group: 'org.jooq', name: 'jooq', version: '3.16.2'
    jooqGenerator group: 'org.postgresql', name: 'postgresql', version: '42.3.1'

    // eval
    implementation group: 'org.codehaus.groovy', name: 'groovy-jsr223', version: '3.0.9'

    // expiring map for caches
    implementation group: 'net.jodah', name: 'expiringmap', version: '0.5.10'

    // jda
    implementation(group: 'com.github.DV8FromTheWorld', name: 'JDA', version: '7ca6980550') {
        exclude group: 'club.minnced', module: 'opus-java'
    }

    // loading commands with reflection
    implementation group: 'io.github.classgraph', name: 'classgraph', version: '4.8.138'

    // logging
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.10'
    implementation group: 'io.sentry', name: 'sentry-logback', version: '5.5.3'

    // music
    implementation group: 'com.github.walkyst', name: 'lavaplayer-fork', version: '61dc03e'
    implementation group: 'com.sedmelluq', name: 'jda-nas', version: '1.1.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation(group: 'com.github.topisenpai', name: 'spotify-plugin', version: 'v1.0.11') {
        exclude group: 'com.github.freyacodes.lavalink', module: 'Lavalink-Server'
    }
    implementation(group: 'dev.arbjerg.lavalink', name: 'plugin-api', version: '0.9.0') {
        exclude group: 'com.github.walkyst', module: 'lavaplayer-fork'
    }
}

jooq {
    version = '3.16.2'
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://localhost:5432/spidey'
                    user = 'sebo'
                    password = DB_PASSWORD
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        includes = '.*'
                        excludes = ''
                    }
                    generate {
                        records = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'dev.mlnr.spidey.jooq'
                        directory = "src/main/jooq"
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}

group = 'dev.mlnr'
description = 'Spidey'

shadowJar.setProperty('archiveFileName', 'Spidey.jar')
compileJava.options.encoding = 'UTF-8'
